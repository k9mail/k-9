apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

if (rootProject.testCoverage) {
    apply plugin: 'jacoco'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    implementation "org.apache.james:apache-mime4j-core:${versions.mime4j}"
    implementation "org.apache.james:apache-mime4j-dom:${versions.mime4j}"
    implementation "com.squareup.okio:okio:${versions.okio}"
    implementation "commons-io:commons-io:${versions.commonsIo}"
    implementation "androidx.annotation:annotation:${versions.androidxAnnotation}"
    implementation "com.jakewharton.timber:timber:${versions.timber}"
    implementation "org.koin:koin-android:${versions.koin}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlinCoroutines}"

    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit}"

    testImplementation project(":mail:testing")
    testImplementation "org.robolectric:robolectric:${versions.robolectric}"
    testImplementation "junit:junit:${versions.junit}"
    testImplementation "com.google.truth:truth:${versions.truth}"
    testImplementation "org.mockito:mockito-core:${versions.mockito}"
}

android {
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    defaultConfig {
        minSdkVersion buildConfig.minSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        // credentials could be created here: https://console.developers.google.com/
        debug {
            testCoverageEnabled rootProject.testCoverage
            buildConfigField "String", "GOOGLE_CLIENT_ID", "\""+getDebugGmailClientId()+"\""
            buildConfigField "String", "GOOGLE_CLIENT_ID_PACKAGE_NAME", "\"com.fsck.k9.debug\""
        }

        release {
            buildConfigField "String", "GOOGLE_CLIENT_ID", "\"*********add release credentials here***************.apps.googleusercontent.com\""
            buildConfigField "String", "GOOGLE_CLIENT_ID_PACKAGE_NAME", "\"com.fsck.k9\""
        }
    }

    lintOptions {
        abortOnError false
        lintConfig file("$rootProject.projectDir/config/lint/lint.xml")
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions {
        jvmTarget = kotlinJvmVersion
    }
}

def getDebugGmailClientId() {
    Properties properties = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        return properties.getProperty("debugGmailClientId")
    } else {
        return "null"
    }
}
