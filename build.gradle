buildscript {
    ext {
        buildConfig = [
                'compileSdk': 28,
                'minSdk': 19,
                'buildTools': '28.0.3'
        ]

        versions = [
                'kotlin': '1.2.71',
                'supportLibrary': '28.0.0',
                'preferencesFix': '28.0.0.0',
                'lifecycleExtensions': '1.1.1',
                'okio': '1.14.0',
                'timber': '4.5.1',
                'koin': '0.9.1',
                'commonsIo': '2.4',
                'coreKtx': '0.3',
                'mime4j': '0.8.1',

                'junit': '4.12',
                'robolectric': '3.7.1',
                'mockito': '2.18.0',
                'mockitoKotlin': '1.5.0',
                'truth': '0.35'
        ]
    }

    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${versions.kotlin}"
    }
}

project.ext {
    testCoverage = project.hasProperty('testCoverage')
}

subprojects {
    repositories {
        mavenCentral()
        google()
        maven { url 'https://jitpack.io' }
        jcenter()
    }

    configurations.all {
        exclude group: "com.android.support", module: "support-media-compat"

        resolutionStrategy {
            force "com.android.support:support-annotations:${versions.supportLibrary}"
            force "com.android.support:design:${versions.supportLibrary}"
            force "com.android.support:support-fragment:${versions.supportLibrary}"
            force "com.android.support:preference-v14:${versions.supportLibrary}"
            force "com.android.support:cardview-v7:${versions.supportLibrary}"

            // org.koin:koin-android-architecture depends on an old version of android.arch.lifecycle:extensions
            force "android.arch.lifecycle:extensions:${versions.lifecycleExtensions}"

            // com.github.amlcurran.showcaseview:library depends on com.android.support:support-v4
            force "com.android.support:support-v4:${versions.supportLibrary}"
        }
    }
}
